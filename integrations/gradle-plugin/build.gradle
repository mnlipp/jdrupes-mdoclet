plugins {
	id "com.jfrog.bintray" version "1.7.3"
	id "com.gradle.plugin-publish" version "0.9.7"
}

apply plugin: 'signing'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: "com.gradle.plugin-publish"

dependencies {
    compile gradleApi()
    compile localGroovy()
}

// Configure sensible layout
sourceSets {
	main {
		groovy {
			srcDir 'src'
		}
		resources {
			srcDir 'resources'
		}
	}
}

processResources {
	//noinspection UnnecessaryQualifiedReference
	filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [
			"org.jdrupes.mdoclet-version": project(':mdoclet').version
	]
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allJava
	classifier "sources"
}
  
task javadocJar(type: Jar) {
	from (project.rootDir) {
		include 'README.md'
	}
	classifier "javadoc"
}

// Allow build without signing information (e.g. travis)
if (project.hasProperty('signing.keyId')) {
	signing {
		sign configurations.archives
	}
}

// Publish to gradle

pluginBundle {
	website = 'https://github.com/mnlipp/jdrupes-mdoclet'
	vcsUrl = 'git@github.com:mnlipp/jdrupes-mdoclet.git'
	description = 'A plugin for configuring MDoclet (a doclet for Markdown javadoc comments)'
	tags = ['javadoc', 'doclet', 'markdown']
  
	plugins {
		mdocletPlugin {
			id = 'org.jdrupes.mdoclet'
			displayName = 'MDoclet configuration plugin'
		}
	}
}
  
// MavenPublishing (new), incomplete, works only for local. 

publishing {
	
	publications {
		mavenJava(MavenPublication) {
			artifactId = project.archivesBaseName
			from components.java
			artifact sourcesJar
			artifact javadocJar
		}
	}
}

// MavenPublishing (original), still needed for remote 

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
 
			pom.project {
				
				name project.name
				artifactId project.archivesBaseName
				packaging 'pom'
				description project.description
				url 'https://github.com/mnlipp/jdrupes-mdoclet'
 
				scm {
					url 'scm:git@github.com:mnlipp/jdrupes-mdoclet.git'
					connection 'scm:git@github.com:mnlipp/jdrupes-mdoclet.git'
					developerConnection 'git@github.com:mnlipp/jdrupes-mdoclet.git'
				}
 
				licenses {
					license {
						name 'GPL 3.0'
						url 'https://www.gnu.org/licenses/gpl-3.0.en.html'
						distribution 'repo'
					}
				}
 
				developers {
					developer {
						id 'mnlipp'
						name 'Michael N. Lipp'
					}
				}
			}
			
			artifacts {
				archives jar
				archives sourcesJar
				archives javadocJar
			}
			
			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: project.properties['sonatypeUsername'] ?: "nouser", 
					password: project.properties['sonatypePassword'] ?: "nopass")
			}
			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: project.properties['sonatypeUsername'] ?: "nouser", 
					password: project.properties['sonatypePassword'] ?: "nopass")
			} 
		}
	}
}

bintray {
	user = project.properties['bintrayUser'] ?: "nouser"
	key = project.properties['bintrayApiKey'] ?: "nokey"
	publications = ['mavenJava']
	publish = true
	pkg {
		repo = "jdrupes"
		name = "org.jdrupes.mdoclet:gradle-plugin"
		version {
			 name = project.version
		}
	}
 }
